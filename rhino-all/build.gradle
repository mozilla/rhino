plugins {
    id 'rhino.library-conventions'
    id 'com.gradleup.shadow' version '9.3.1'
}

dependencies {
    implementation project(':rhino')
    implementation project(':rhino-tools')
    implementation project(':rhino-xml')
    // Include JLine so that the stand-alone JAR gets a nice terminal
    runtimeOnly libs.jline.terminal
    runtimeOnly libs.jline.reader
    runtimeOnly libs.bundles.jline.runtime
}

shadowJar {
    // Ensure that the "jar" from this step is the shadowed one that we want to 
    // publish in Maven.
    archiveClassifier.set('')
    // Merge service files, but fail on other duplicates.
    duplicatesStrategy = DuplicatesStrategy.INCLUDE // Or WARN.
    mergeServiceFiles()
    filesNotMatching('META-INF/services/**') {
        duplicatesStrategy = DuplicatesStrategy.FAIL // Or FAIL.
    }
    manifest {
        attributes 'Main-Class': 'org.mozilla.javascript.tools.shell.Main'
    }
}

publishing {
    publications {
        rhinoall(MavenPublication) {
            from components.java
            artifacts = [jar, sourceJar, javadocJar]
            pom.withXml {
                def root = asNode()

                root.appendNode('name', 'rhino-all')
                root.appendNode('description', "Rhino JavaScript all-in-one JAR, not for use with modular Java projects")
                root.appendNode("url", "https://mozilla.github.io/rhino/")

                def p = root.appendNode("parent")
                p.appendNode("groupId", "org.sonatype.oss")
                p.appendNode("artifactId", "oss-parent")
                p.appendNode("version", "7")

                def l = root.appendNode("licenses").appendNode("license")
                l.appendNode("name", "Mozilla Public License, Version 2.0")
                l.appendNode("url", "http://www.mozilla.org/MPL/2.0/index.txt")

                def scm = root.appendNode("scm")
                scm.appendNode("connection", "scm:git:git@github.com:mozilla/rhino.git")
                scm.appendNode("developerConnection", "scm:git:git@github.com:mozilla/rhino.git")
                scm.appendNode("url", "git@github.com:mozilla/rhino.git")

                def o = root.appendNode("organization")
                o.appendNode("name", "The Mozilla Foundation")
                o.appendNode("url", "http://www.mozilla.org")

                def d = root.appendNode("developers")
                def dd = d.appendNode("developer")
                dd.appendNode("name", "Greg Brail")
                dd.appendNode("email", "gbrail@users.noreply.github.com")
            }
        }
    }
}
