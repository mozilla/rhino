plugins {
    id 'rhino.library-conventions'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'application'
}

application {
    mainClass = 'org.mozilla.javascript.tools.shell.Main'
}

run {
    standardInput = System.in
}

dependencies {
    implementation project(':rhino')
    implementation project(':rhino-tools')
    implementation project(':rhino-xml')
}

shadowJar {
    // Ensure that the "jar" from this step is the shadowed one that we want to 
    // publish in Maven.
    archiveClassifier.set('')
    manifest {
      inheritFrom(project.tasks.jar.manifest)
    }
}

startScripts {
    dependsOn 'shadowJar'
}

startShadowScripts {
    dependsOn 'jar'
}

publishing {
    publications {
        rhinoall(MavenPublication) {
            from components.java
            artifacts = [jar]
            pom {
                description = "Rhino JavaScript all-in-one JAR, not for use with modular Java projects"
                url = "https://mozilla.github.io/rhino/"
                licenses {
                    license {
                        name = "Mozilla Public License, Version 2.0"
                        url = "http://www.mozilla.org/MPL/2.0/index.txt"
                    }
                }
                scm {
                    connection = "scm:git:git@github.com:mozilla/rhino.git"
                    developerConnection = "scm:git:git@github.com:mozilla/rhino.git"
                    url = "git@github.com:mozilla/rhino.git"
                }
                organization {
                    name = "The Mozilla Foundation"
                    url = "http://www.mozilla.org"
                }
            }
        }
    }
}
