plugins {
    id 'rhino.library-conventions'
    id 'org.jetbrains.kotlin.jvm'
}

dependencies {
    implementation project(':rhino')
    implementation "org.jetbrains.kotlin:kotlin-metadata-jvm:2.2.20"

    testImplementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:2.2.20"
}

kotlin {
    jvmToolchain(11)
}

publishing {
    publications {
        rhinokotlin(MavenPublication) {
            from components.java
            artifacts = [jar, sourceJar]
            pom.withXml {
                def root = asNode()

                root.appendNode('description', "Rhino reflection interfaces implementations for Kotlin")
                root.appendNode("url", "https://mozilla.github.io/rhino/")

                def p = root.appendNode("parent")
                p.appendNode("groupId", "org.sonatype.oss")
                p.appendNode("artifactId", "oss-parent")
                p.appendNode("version", "7")

                def l = root.appendNode("licenses").appendNode("license")
                l.appendNode("name", "Mozilla Public License, Version 2.0")
                l.appendNode("url", "http://www.mozilla.org/MPL/2.0/index.txt")

                def scm = root.appendNode("scm")
                scm.appendNode("connection", "scm:git:git@github.com:mozilla/rhino.git")
                scm.appendNode("developerConnection", "scm:git:git@github.com:mozilla/rhino.git")
                scm.appendNode("url", "git@github.com:mozilla/rhino.git")

                def o = root.appendNode("organization")
                o.appendNode("name", "The Mozilla Foundation")
                o.appendNode("url", "http://www.mozilla.org")
            }
        }
    }
}
