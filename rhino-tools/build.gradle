plugins {
    id 'rhino.library-conventions'
}

dependencies {
    implementation project(':rhino')
    compileOnly 'org.jline:jline-terminal:3.30.0'
    compileOnly 'org.jline:jline-reader:3.30.0'
    testImplementation project(':testutils')
}

publishing {
    publications {
        rhinotools(MavenPublication) {
            from components.java
            artifacts = [jar, sourceJar, javadocJar]
            pom.withXml {
                def root = asNode()

                root.appendNode('name', 'rhino-tools')
                root.appendNode('description', "Rhino tools, including shell and debugger")
                root.appendNode("url", "https://mozilla.github.io/rhino/")

                def p = root.appendNode("parent")
                p.appendNode("groupId", "org.sonatype.oss")
                p.appendNode("artifactId", "oss-parent")
                p.appendNode("version", "7")

                def l = root.appendNode("licenses").appendNode("license")
                l.appendNode("name", "Mozilla Public License, Version 2.0")
                l.appendNode("url", "http://www.mozilla.org/MPL/2.0/index.txt")

                def scm = root.appendNode("scm")
                scm.appendNode("connection", "scm:git:git@github.com:mozilla/rhino.git")
                scm.appendNode("developerConnection", "scm:git:git@github.com:mozilla/rhino.git")
                scm.appendNode("url", "git@github.com:mozilla/rhino.git")

                def o = root.appendNode("organization")
                o.appendNode("name", "The Mozilla Foundation")
                o.appendNode("url", "http://www.mozilla.org")

                def d = root.appendNode("developers")
                def dd = d.appendNode("developer")
                dd.appendNode("name", "Greg Brail")
                dd.appendNode("email", "gbrail@users.noreply.github.com")
            }
        }
    }
}
