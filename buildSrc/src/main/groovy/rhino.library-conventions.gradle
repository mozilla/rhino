// This file inherits from "java-conventions" and adds additional error
// checks and publishing data for modules that will be published to
// Maven Central. It should only be used for those modules that will
// be published and used by other projects.

plugins {
    id 'rhino.java-conventions'
    id 'maven-publish'
    id 'signing'
    id 'jacoco'
    id 'net.ltgt.errorprone'
    id 'de.obqo.decycle'
}

dependencies {
    errorprone "com.google.errorprone:error_prone_core:2.45.0"
}

version = project.version

tasks.named('compileJava') {
    options.javaModuleVersion = provider { version }
}

tasks.withType(Jar).configureEach {
    manifest {
        attributes (
            "Implementation-Version": project.version,
            "Implementation-Title":  "Mozilla Rhino",
            "Implementation-Vendor": "Mozilla Foundation",
            "Implementation-URL": "http://github.com/mozilla/rhino",
        )
    }
}

tasks.withType(JavaCompile).configureEach {
    if (JavaVersion.current() < JavaVersion.VERSION_17) {
        // Latest versions of ErrorProne are only compatible with Java 17
        // and up.
        System.out.println("Compiling with Java " + JavaVersion.current() +
          " ErrorProne checks disabled");
        options.errorprone.enabled = false;
    } else {
        // Disable tests that we aren't ready for in ErrorProne.
        options.errorprone.disable(
                // Things we should fix as we increase Java versions
                "ClassInitializationDeadlock",
                "EffectivelyPrivate",
                "BooleanLiteral",
                "AssignmentExpression",
                "DuplicateBranches",
                "PatternMatchingInstanceof",
                "RedundantControlFlow",
                // JavaDoc stuff -- not going to fix unless we do a big JavaDoc update
                "AlmostJavadoc",
                "EmptyBlockTag",
                "EscapedEntity",
                "MissingSummary",
                "InvalidBlockTag",
                "NotJavadoc",
                "UnicodeEscape",
                // Stuff that we just love to do but should do less of eventually
                "EmptyCatch",
                "LabelledBreakTarget",
                "JavaUtilDate",
                // Less important for now, more stylistic than bug
                "InlineMeSuggester",
                // This one either alerts for parameters that we don't use,
                // or spuriously for local variables in my opinion.
                "UnusedVariable",
                // This requires annotations that are difficult to incorporate
                // without adding a dependency for everyon who uses Rhino.
                "AnnotateFormatMethod"
        )
        options.errorprone.enable(
                // These two do the last thing that Checkstyle used to do for us
                "RemoveUnusedImports",
                "WildcardImport")
        options.errorprone.excludedPaths = '.+/src/test/java/.+'
    }
}

tasks.withType(Javadoc) {
      failOnError = false
      options.addStringOption('Xdoclint:all,-missing', '-quiet')
}

jacocoTestReport {
    reports {
        xml.required = false
        csv.required = false
        html.outputLocation = layout.buildDirectory.dir('reports/jacoco')
    }
}

task javadocJar(type: Jar) {
    archiveClassifier = 'javadoc'
    from javadoc
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
    from ('LICENSE.txt') {
        into 'META-INF'
    }
    from ('NOTICE.txt') {
        into 'META-INF'
    }
    from ('NOTICE-tools.txt') {
        into 'META-INF'
    }
}

signing {
    if (project.hasProperty('SIGNINGKEY')) {
        // Check for ORG_GRADLE_PROJECT_SIGNINGKEY environment variable for use in CI system.
        // Otherwise, do not sign.
        def signingKey = getProperty('SIGNINGKEY')
        def signingPassword = getProperty('SIGNINGPASSWORD')
        useInMemoryPgpKeys(signingKey, signingPassword)
        sign publishing.publications
    }
}


publishing {
    if (project.hasProperty("mavenPassword")) {
        repositories {
            maven {
                credentials {
                    username = mavenUser
                    password = mavenPassword
                }
                if (project.version.endsWith('-SNAPSHOT')) {
                    url = mavenSnapshotRepo
                } else {
                    url = mavenReleaseRepo
                }
            }
        }
    } else if (System.getenv("GITHUB_TOKEN") != null) {
        repositories {
            maven {
                credentials {
                    username = System.getenv("GITHUB_ACTOR")
                    password = System.getenv("GITHUB_TOKEN")
                }
                url = githubPackagesRepo
            }
        }
    }
}
