plugins {
    id 'rhino.java-conventions'
    id 'com.github.spotbugs'
    id 'maven-publish'
    id 'checkstyle'
}

import com.github.spotbugs.snom.Confidence
import com.github.spotbugs.snom.Effort

version = project.version

tasks.named('compileJava') {
    options.javaModuleVersion = provider { version }
}

tasks.withType(Jar).configureEach {
    manifest {
        attributes (
            "Implementation-Version": project.version,
            "Implementation-Title":  "Mozilla Rhino",
            "Implementation-Vendor": "Mozilla Foundation",
            "Implementation-URL": "http://github.com/mozilla/rhino",
        )
    }
}

spotbugs {
    effort = Effort.valueOf('LESS')
    reportLevel = Confidence.valueOf('MEDIUM')
    excludeFilter = file("${projectDir}/../config/spotbugs/spotbugs-exclude.xml")
}

spotbugsMain {
    reports {
        html {
            required = true
            outputLocation = file("$buildDir/reports/spotbugs/main/spotbugs.html")
            stylesheet = 'fancy-hist.xsl'
        }
    }
}

task javadocJar(type: Jar) {
    archiveClassifier = 'javadoc'
    from javadoc
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
    from ('LICENSE.txt') {
        into 'META-INF'
    }
    from ('NOTICE.txt') {
        into 'META-INF'
    }
    from ('NOTICE-tools.txt') {
        into 'META-INF'
    }
}
