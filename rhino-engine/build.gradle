plugins {
    id 'rhino.library-conventions'
}

dependencies {
    implementation project(':rhino')
    testImplementation project(':testutils')
}

jar {
    manifest {
        attributes(
            "Bundle-ManifestVersion": "2",
            "Bundle-SymbolicName": "org.mozilla.rhino.engine",
            "Bundle-Version": osgiVersion,
            "Bundle-Vendor": "Mozilla Foundation",
            "Bundle-License": "https://www.mozilla.org/MPL/2.0/",
            "Export-Package": "org.mozilla.javascript.engine;version=\"${osgiVersion}\"",
            "Import-Package": [
                "javax.script",
                "org.mozilla.javascript;version=\"${osgiVersion}\"",
            ].join(","),
            "Provide-Capability": "osgi.serviceloader;osgi.serviceloader=\"javax.script.ScriptEngineFactory\"",
            "Require-Capability": [
                "osgi.ee;filter:=\"(&(osgi.ee=JavaSE)(version>=11))\"",
                "osgi.extender;filter:=\"(osgi.extender=osgi.serviceloader.registrar)\"",
            ].join(","),
        )
    }
}

publishing {
    publications {
        rhinoengine(MavenPublication) {
            from components.java
            artifacts = [jar, sourceJar, javadocJar]
            pom.withXml {
                def root = asNode()

                root.appendNode('name', 'rhino-engine')
                root.appendNode('description', "Rhino ScriptEngine implementation")
                root.appendNode("url", "https://mozilla.github.io/rhino/")

                def p = root.appendNode("parent")
                p.appendNode("groupId", "org.sonatype.oss")
                p.appendNode("artifactId", "oss-parent")
                p.appendNode("version", "7")

                def l = root.appendNode("licenses").appendNode("license")
                l.appendNode("name", "Mozilla Public License, Version 2.0")
                l.appendNode("url", "http://www.mozilla.org/MPL/2.0/index.txt")

                def scm = root.appendNode("scm")
                scm.appendNode("connection", "scm:git:git@github.com:mozilla/rhino.git")
                scm.appendNode("developerConnection", "scm:git:git@github.com:mozilla/rhino.git")
                scm.appendNode("url", "git@github.com:mozilla/rhino.git")

                def o = root.appendNode("organization")
                o.appendNode("name", "The Mozilla Foundation")
                o.appendNode("url", "http://www.mozilla.org")

                def d = root.appendNode("developers")
                def dd = d.appendNode("developer")
                dd.appendNode("name", "Greg Brail")
                dd.appendNode("email", "gbrail@users.noreply.github.com")
            }
        }
    }
}
