buildscript {
    repositories {
        gradlePluginPortal()
        google()
        mavenCentral()
        mavenLocal()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.9.3'
    }
}

plugins {
    id 'rhino.spotless-conventions'
}

repositories {
    google()
    mavenCentral()
}

spotless {
    java {
        target "src/*/java/**/*.java"
    }
}


// read the (android specific) local.properties file
def localPropertiesFile = rootProject.file("local.properties");
Properties localProperties = new Properties()
if (localPropertiesFile.exists()) {
    localProperties.load(localPropertiesFile.newDataInputStream())
}

if (!System.getenv("ANDROID_HOME") && !localProperties.get("sdk.dir")) {
    System.out.println("No Android SDK found. Skipping build")
} else {
    apply plugin: 'com.android.application'

    android {
        namespace = "org.mozilla.javascript.android"
        compileSdkVersion 33
        defaultConfig {
            minSdk = 21
            targetSdk = 33
            testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"

        }
        compileOptions {
            coreLibraryDesugaringEnabled true
            sourceCompatibility JavaVersion.VERSION_8
            targetCompatibility JavaVersion.VERSION_8
        }
    }

    dependencies {
        implementation project(':rhino-engine')
        implementation project(':examples')
        implementation project(':rhino')
        implementation project(':rhino-tools')
        implementation project(':rhino-xml')

        coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.0.9'

        testImplementation 'junit:junit:4.13.2'
        androidTestImplementation 'androidx.test:runner:1.7.0'
        androidTestImplementation 'androidx.test.ext:junit:1.3.0'
    }
}
